program    = define*

define     = base_type ident (func | type_suffix ";")
base_type  = ("int" | "char") ("*")*
type_suffix= ( ("[" num "]") type_suffix )?
func       = "(" params? ")" block
params     = type ident ("," params)*
block      = "{" stmt* "}"

stmt       = expr ";"
             | block
             | "return" expr ";"
             | "if" "(" expr ")" stmt ("else" stmt)?
             | "while" "(" expr ")" stmt
             | "for" "(" expr? ";" expr? ";" expr? ")" stmt
             | base_type ident type_suffix ";"

expr       = assign
assign     = equality ("=" assign)?
equality   = relational ("==" relational | "!=" relational)*
relational = add ("<" add | "<=" add | ">" add | ">=" add)*
add        = mul ("+" mul | "-" mul)*
mul        = unary ("*" unary | "/" unary)*
unary      = "sizeof" unary
             | ("+" | "-")? primary
             | ("*" | "&") unary
primary    = ident ("(" args? ")" | idx_access)? | "(" expr ")" | num
args       = expr ("," args)*
idx_access = "[" expr "]" idx_access*
